/*USING INBUILT CLASS DATASET TO CREATE A TABLE BY FOLLOWING MOCK SHELL*/

                  /*CREATING TOTAL CATEGORY*/
data DOUBLE;
    set sashelp.class;
    output;
    Sex = 'T';
    output;
run;

       /*CRATING STATISTICS SEPARATELY FOR AGE HEIGHT AND WEIGHT VARIABLE*/

%macro sstep (var=);

proc means data=double nway noprint;
    var &var;
    class sex;
    output out = &var.stat(drop=_:) 
    n=N std=SD max=MAX min=MIN mean=MEAN median=MEDIAN;
    run ;

proc transpose data=&var.stat out=&var.transs;
    id sex;
run;

DATA &var.STREADY;
    length Parameter$ 7;
    RETAIN Parameter _NAME_ F M T;
    SET &var.transs;
    IF _NAME_='N' THEN Parameter="&var";
RUN ;

%mend;

%sstep (var=AGE);
%sstep (var=HEIGHT);
%sstep (var=WEIGHT);

              /*COMBINING 3 SEPARATELY CREATED DATASETS*/
                      /*CONTROLLING DECIMALS*/

data REPORTREADY ;
    set AGESTREADY HEIGHTSTREADY WEIGHTSTREADY;
    NAME=UPCASE(_NAME_);
%MACRO DECIMAL (N,D);
    if NAME=&N then DO ;
    FEMALE = put(F,7.&D.);
    MALE   = put(M,7.&D.);
    TOTAL  = put(T,7.&D.);
    END;   %MEND DECIMAL ;
%DECIMAL(N='N',D=0);
%DECIMAL(N='SD',D=2);
%DECIMAL(N='MAX',D=0);
%DECIMAL(N='MIN',D=0);
%DECIMAL(N='MEAN',D=1);
%DECIMAL(N='MEDIAN',D=2);
    DROP _NAME_ F M T;
run;

           /*FINAL STEP CREATING CUSTOMIZED REPORT*/

 ODS RTF FILE="/home/u62043545/SOURCE/TABLE.RTF" STYLE=MINIMAL;
PROC REPORT DATA=REPORTREADY HEADLINE HEADSKIP NOWD 
    STYLE=[FRAME=HSIDES RULES=GROUPS  FONT_FACE="TIMES NEW ROMAN" FONT_SIZE=8 PT] ;
    COLUMN PARAMETER NAME FEMALE MALE TOTAL;
    DEFINE PARAMETER/'PARAMETER' ORDER WIDTH=9;
    DEFINE NAME/ 'STAT' DISPLAY;
    DEFINE FEMALE/'FEMALE' DISPLAY;
    DEFINE MALE/'MALE' DISPLAY;
    DEFINE TOTAL/'TOTAL' DISPLAY;
    TITLE    'SUMMARY REPORT OF STUDENTS';
    FOOTNOTE 'GENERATED BY KRISHNA';
RUN;
 ODS RTF CLOSE;

/********************************************************************************************************/
