                        /* LIBRARY DEFINATION */

LIBNAME RAW_D "/home/u62043545/raw_d";

     /* DERIVING 13 VARS FROM RAW DM DATASET WITHOUT MAJOR DEVIATIONS */

DATA EVARS;
    SET RAW_D.DM;

    STUDYID= "XXX-YYY-103";
    DOMAIN="DM";
    SITEID=SITE;
    USUBJID=CATX('-',STUDYID,SITEID,SUBJECTID);
    RFICDTC=PUT(DSSTDAT,IS8601DA.);
    BRTHDTC=PUT(BRTHDAT,IS8601DA.);
    AGEU="YEARS";

    IF  UPCASE(SEX)="MALE" THEN SEX="M";
    ELSE IF UPCASE(SEX)="FEMALE" THEN SEX="F";

    ETHNIC=UPCASE(ETHNIC);
     COUNTRY="USA";

    IF FIND(RACE,'/')>0 THEN RACE="MULTIPLE";
    ELSE RACE=UPCASE(RACE);

    KEEP SUBJECTID STUDYID DOMAIN SITEID USUBJID RFICDTC BRTHDTC AGE AGEU SEX
     ETHNIC COUNTRY RACE;
RUN;

                         /* RFSTDTC RFXSTDTC */

PROC SORT DATA=Raw_d.Ex OUT=SRTDSD;
    BY SUBJECTID EXADTE EXSITIM;
RUN;

DATA STDT;
    SET SRTDSD;
    BY SUBJECTID;
    IF FIRST.SUBJECTID;
    RFSTDTC=CATX("T",PUT(EXADTE,IS8601DA.),PUT(INPUT(EXSITIM,TIME5.),TOD5.));
    RFXSTDTC=RFSTDTC;
    KEEP SUBJECTID RFSTDTC RFXSTDTC;
RUN;

                      /* RFXENDTC */

DATA XENDT ;
    SET SRTDSD;
    BY SUBJECTID;
    IF LAST.SUBJECTID;
    RFXENDTC=CATX("T",PUT(EXADTE,IS8601DA.),PUT(INPUT(EXSITIM,TIME5.),TOD5.));
    KEEP SUBJECTID RFXENDTC;
RUN;

                    /* RFPENDTC RFENDTC */

DATA ALLDATE;
    LENGTH FORM SUBJECTSTATUS $200.;
    SET RAW_D.VS  (RENAME=(VSDAT=DATE)) 
     	  RAW_D.PK  (RENAME=(PCDAT2=DATE))
	     RAW_D.PK  (RENAME=(PCDAT3=DATE))
	     RAW_D.PK  (RENAME=(PCDAT4=DATE))
	     RAW_D.PK  (RENAME=(PCDAT5=DATE))
	     RAW_D.PK1 (RENAME=(PK1DAT=DATE))
	     RAW_D.PK1 (RENAME=(PK1DAT1=DATE))
	     RAW_D.PK1 (RENAME=(PK1DAT2=DATE))
	     RAW_D.PK1 (RENAME=(PK1DAT3=DATE))
	     RAW_D.DS  (RENAME=(DSDT=DATE)) 
	     RAW_D.AE  (RENAME=(AEENDAT=DATE))
	     RAW_D.EG  (RENAME=(EGDAT=DATE)) 
	     RAW_D.EG  (RENAME=(EGDAT1=DATE)) 
	     RAW_D.LB  (RENAME=(LBDAT1=DATE)) 
	     RAW_D.LB  (RENAME=(LBDAT4=DATE));

	 KEEP SUBJECTID DATE;
RUN;

DATA ALLTIME ;
    LENGTH FORM SUBJECTSTATUS $200. ;
    SET RAW_D.VS  (RENAME=(VSTIM=TIME))   RAW_D.PK  (RENAME=(PCTIM2=TIME)) 
	     RAW_D.PK  (RENAME=(PCTIM3=TIME))  RAW_D.PK  (RENAME=(PCTIM4=TIME)) 
	     RAW_D.PK  (RENAME=(PCTIM5=TIME))  RAW_D.PK1 (RENAME=(PK1TIM=TIME)) 
	     RAW_D.PK1 (RENAME=(PK1TIM1=TIME)) RAW_D.PK1 (RENAME=(PK1TIM2=TIME)) 
        RAW_D.PK1 (RENAME=(PK1TIM3=TIME)) RAW_D.AE  (RENAME=(AEENTIM=TIME)) 
	     RAW_D.EG  (RENAME=(EGTIME=TIME))  RAW_D.EG  (RENAME=(EGTIM1=TIME)) 
	     RAW_D.LB  (RENAME=(LBTIM1=TIME))  RAW_D.LB  (RENAME=(LBTIM4=TIME)) 
	     RAW_D.LB  (RENAME=(LBTIM3=TIME))  RAW_D.LB  (RENAME=(LBTIM2=TIME));

	 KEEP SUBJECTID TIME;
RUN;

PROC SORT DATA=ALLDATE OUT=DATE NODUPKEY;
    BY SUBJECTID DATE ;
RUN ;

PROC SORT DATA=ALLTIME OUT=TIME NODUPKEY;
    BY SUBJECTID TIME ;
RUN ;

DATA ADT;
    MERGE DATE TIME;
    BY SUBJECTID;
    IF DATE NE . AND TIME NE '';
RUN;

DATA PNDT;
    SET ADT;
    BY SUBJECTID;
    IF LAST.SUBJECTID;
    RFPENDTC=CATX("T",PUT(DATE,IS8601DA.),PUT(INPUT(TIME,TIME5.),TOD5.));
    RFENDTC=RFPENDTC; 
    KEEP SUBJECTID RFENDTC RFPENDTC;
RUN;

DATA EXTRADT ;
    SET RAW_D.SF ;
    RFPENDTC=PUT(SFDT,IS8601DA.);
    RFENDTC=PUT(SFDT,IS8601DA.);
    KEEP SUBJECTID RFPENDTC RFENDTC;
RUN;

DATA PENDTC;
    SET PNDT EXTRADT;
RUN;

                      /* DERIVING ARM DETAILS */

DATA ARM (KEEP=SUBJECTID ARM);
    SET Raw_d.Sn;
    IF ENRTYPE=:"1.5" THEN ARM="XXX-YYY-1";
    ELSE IF ENRTYPE=:"3.0" THEN ARM="XXX-YYY-2";
    ELSE IF ENRTYPE=:"6.0" THEN ARM="XXX-YYY-3";
RUN ;

DATA ACTARM (KEEP=SUBJECTID ACTARM);
    SET Raw_d.Ex;
    IF EXTOTAD=1.5 THEN ACTARM="XXX-YYY-1";
    ELSE IF EXTOTAD=3.0 THEN ACTARM="XXX-YYY-2";
    ELSE IF EXTOTAD=6.0 THEN ACTARM="XXX-YYY-3";
RUN; 
              
DATA SRF (KEEP=SUBJECTID ACTARM);
    SET Raw_d.Sf;
    IF SFRSN NE "" THEN ACTARM="SCREEN FAILURE";
RUN;

DATA ACTVAR;
    LENGTH ACTARM $20.;
    SET ACTARM SRF;
RUN;     

PROC SORT DATA=ARM NODUP;
    BY SUBJECTID ;
RUN;

PROC SORT DATA=ACTVAR NODUP;
    BY SUBJECTID;
RUN;

DATA ALLARM;
    MERGE ARM ACTVAR;
    BY SUBJECTID;
RUN;

DATA ARMALL;
    LENGTH ACTARMCD $20. ARMCD $20. ARM $20.;
    SET ALLARM;
    IF ARM="XXX-YYY-1" THEN ARMCD="XXX-1";
    ELSE IF ARM="XXX-YYY-2" THEN ARMCD="XXX-2";
    ELSE IF ARM="XXX-YYY-3" THEN ARMCD="XXX-3";
    ACTARMCD=ARMCD;
    IF ACTARM="SCREEN FAILURE" THEN DO ;
	    ACTARMCD="SCRNFAIL";
	    ARMCD="SCRNFAIL" ;
	    ARM= "SCREEN FAILURE" ;
END;
RUN;

                        /* DEATH DETAILS */

DATA DEATH1;
    SET RAW_D.DS;
    IF UPCASE(DSOTHSPC) NE "DEATH" THEN DTHDTC=""; 
    KEEP SUBJECTID DTHDTC;
RUN;

DATA DEATH2;
    SET RAW_D.AE;
    IF AESERCAT NE "Death" OR AEOUT NE "Death" OR AEGRISR NE "Death" THEN
    DTHDTC="";
    KEEP SUBJECTID DTHDTC;
RUN;

DATA DEATH3;
    SET DEATH1 DEATH2;
RUN ;

PROC SORT DATA=DEATH3 OUT=DEATH4;
    BY SUBJECTID DTHDTC;
RUN;

DATA DEATHDET;
    SET DEATH4;
    BY SUBJECTID;
    IF LAST.SUBJECTID;
    IF DTHDTC="" THEN DTHFL="";
    ELSE DTHFL="Y";
RUN;

             /* SORTING ALL CREATED DATASETS FOR MERGE */

PROC SORT DATA=EVARS  ;
    BY SUBJECTID;
RUN;
     
PROC SORT DATA=STDT ;
    BY SUBJECTID;
RUN;
    
PROC SORT DATA=XENDT ;
    BY SUBJECTID;
RUN;

PROC SORT DATA=PENDTC;
    BY SUBJECTID;
RUN;

PROC SORT DATA=ARMALL;
    BY SUBJECTID;
RUN;

PROC SORT DATA=DEATHDET ;
    BY SUBJECTID;
RUN;

         /* MERGING TO GET ALL THE REQUIRED VARIABLES */

DATA DMFINAL;
    MERGE EVARS STDT XENDT PENDTC ARMALL DEATHDET;
    BY SUBJECTID;
RUN;

         /* CREATING MACRO FOR ALL REQUIRED VARIABLES */

%LET RVARS= STUDYID DOMAIN USUBJID SUBJID RFSTDTC RFENDTC RFXSTDTC RFXENDTC RFICDTC
            RFPENDTC DTHDTC DTHFL SITEID BRTHDTC AGE AGEU SEX RACE ETHNIC ARMCD ARM 
            ACTARMCD ACTARM COUNTRY;  

                        /* FINAL STEP */

DATA DM(LABEL=DEMOGRAPHICS);
     RETAIN &RVARS;
     SET DMFINAL(RENAME=(SUBJECTID=SUBJID));
	  keep &RVARS;
	  label STUDYID="Study Identifier" 
		     DOMAIN="Domain Abbreviation" 
		     USUBJID="Unique Subject Identifier" 
		     SUBJID="Subject Identifier for the Study" 
		     RFSTDTC="Subject Reference Start Date/Time" 
		     RFENDTC="Subject Reference End Date/Time" 
		     RFXSTDTC="Date/Time of First Study Treatment" 
		     RFXENDTC="Date/Time of Last Study Treatment" 
	     	  RFICDTC="Date/Time of Informed Consent" 
		     RFPENDTC="Date/Time of End of Participation" 
		     DTHDTC="Date/Time of Death" 
		     DTHFL="Subject Death Flag" 
		     SITEID="Study Site Identifier" 
		     BRTHDTC="Date/Time of Birth" 
		     AGE="Age" 
		     AGEU="Age Units" 
		     SEX="Sex" 
		     RACE="Race" 
		     ETHNIC="Ethnicity" 
		     ARMCD="Planned Arm Code" 
		     ARM="Description of Planned Arm" 
		     ACTARMCD="Actual Arm Code" 
		     ACTARM="Description of Actual Arm" 
		     COUNTRY="Country";
RUN;

/*******************************************************************************************/
