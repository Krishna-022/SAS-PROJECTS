
                               /*LIBRABY DEFINATION*/

LIBNAME ADAM "/home/u62043545/NEWDATA";

                            /*KEEPING REQUIRED VARIABLES*/

DATA RV;
    SET ADAM.ADMH;
    KEEP USUBJID MHSEQ SUBDISP COHORT COHORTN DOSE 
         MHDECOD MHSTDTC MHENDTC MHENRF MHCLSIG;
RUN;

              /*INTERPRETATION OF MISSING DATE VALUES AND DERIVING ALL 6 COL*/

DATA COL;
    SET RV;

    COL1=SUBDISP;
    COL2='Cohort'||' '||STRIP(PUT(COHORTN,BEST.))||'/'||STRIP(DOSE);
    COL3=MHDECOD;

    IF NOT MISSING(MHSTDTC) AND LENGTH(MHSTDTC)=4 THEN 
    ASTDT=INPUT(CATX('-',MHSTDTC,'01','01'),YYMMDD10.);

    ELSE IF NOT MISSING(MHSTDTC) AND LENGTH(MHSTDTC)=7 THEN
    ASTDT=INPUT(CATX('-',MHSTDTC,'01'),YYMMDD10.);

    ELSE IF NOT MISSING(MHSTDTC) AND LENGTH(MHSTDTC)=10 THEN
    ASTDT=INPUT(STRIP(MHSTDTC),YYMMDD10.);

    IF NOT MISSING(MHENDTC) AND LENGTH(MHENDTC)=4 THEN 
    AENDT=INPUT(CATX('-',STRIP(MHENDTC),'12','30'),YYMMDD10.);

    IF NOT MISSING(MHENDTC) AND LENGTH(MHENDTC)=7 AND 
    SUBSTR(MHENDTC,6,2)='02' THEN AENDT=INPUT(CATX('-',STRIP(MHENDTC),'28'),YYMMDD10.);
    ELSE IF NOT MISSING(MHENDTC) AND LENGTH(MHENDTC)=7 AND 
    SUBSTR(MHENDTC,6,2) IN('04','06','09','11') THEN 
    AENDT=INPUT(CATX('-',STRIP(MHENDTC),'30'),YYMMDD10.); 
    ELSE IF NOT MISSING(MHENDTC) AND LENGTH(MHENDTC)=7 THEN 
    AENDT=INPUT(CATX('-',STRIP(MHENDTC),'31'),YYMMDD10.);

    IF NOT MISSING(MHENDTC) AND LENGTH(MHENDTC)=10 THEN
    AENDT=INPUT(STRIP(MHENDTC),YYMMDD10.);

    COL4='S:'||' '||STRIP(PUT(ASTDT,DATE9.))||' '||'E:'||' '||STRIP(PUT(AENDT,DATE9.));

    IF UPCASE(MHENRF)='ONGOING' THEN COL5='Yes';
    ELSE COL5='No';

    IF UPCASE(MHCLSIG)="Y" THEN COL6 ="Yes" ;
    ELSE IF UPCASE(MHCLSIG)="N" THEN COL6 ="No" ; 

   KEEP USUBJID MHSEQ COL1-COL6 ;
RUN;

PROC SORT DATA=COL;
    BY USUBJID MHSEQ ;
RUN;

DATA DO ;
    SET COL;
    RETAIN C 0;
    D+1;
    IF D>6 THEN DO;
    D=1;
    C=C+1;
END;
RUN;

                           /*LABEL DEFINATION*/

DATA RREADY;
    SET DO;
    LABEL COL1="Subject/Id/Age/Gender/Race"
          COL2="Cohort/Dose"
          COL3="Medical Condition/Description"
          COL4="S: Start Date E: Stop Date"
          COL5="Ongoing?"
          COL6="Clinically Significant?";
	 KEEP C COL: ;
RUN;

                    /*SPECIFYING TITLES AND FORMATTING*/

OPTIONS NODATE NONUMBER ORIENTATION=LANDSCAPE PAPERTYPE=A4
        LEFTMARGIN=1IN RIGHTMARGIN=1IN TOPMARGIN=1IN BOTTOMMARGIN=1IN ;

TITLE J=L FONT="COURIER NEW" HEIGHT=9PT "Study Code: XXX-YYY-103" 
      J=RIGHT "Private and Confidential";

TITLE2 J=L FONT='COURIER NEW' HEIGHT=9PT "Statistical Analysis Plan"
       J=RIGHT FONT='COURIER NEW' HEIGHT=9PT "DATE: &SYSDATE";

TITLE3 J=L FONT='COURIER NEW' HEIGHT=9PT "FINAL VERSION 1.0" 
       J=RIGHT FONT='COURIER NEW' HEIGHT=9PT "Page X of Y";

TITLE4 J=CENTER FONT='COURIER NEW' HEIGHT=9PT "Listing 16.2.4.2";
TITLE5 J=CENTER FONT='COURIER NEW' HEIGHT=9PT "Listing of Medical History";
TITLE6 J=CENTER FONT='COURIER NEW' HEIGHT=9PT "Safety Population";

                      /*CREATING FINAL REPORT*/

ODS RTF FILE="/home/u62043545/LIsting code/FINAL_LISTING.RTF" STYLE=JOURNAL;

PROC REPORT DATA=RREADY SPLIT='*'
    STYLE(REPORT)={CELLWIDTH=100% CELLPADDING=0.5% CELLSPACING=5} 
    STYLE(HEADER)={VJUST=T FONTWEIGHT=BOLD FONT=("COURIER NEW") FONTSIZE=9PT} 
	STYLE(COLUMN)={VJUST=T PADDINGBOTTOM=4MM FONT=("COURIER NEW") FONTSIZE=9PT} ;
    COLUMN C COL1 COL2 COL3 COL4 COL5 COL6;
    DEFINE C/GROUP NOPRINT;

    DEFINE COL1/GROUP "SubjectId/*Age/*Gender/*Race"
	STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=10%};

    DEFINE COL2/GROUP "Cohort/Dose"
	STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=12%};

    DEFINE COL3/ "Medical Condition/Description"
    STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=19%};

    DEFINE COL4/ "S: Start Date*E: Stop Date" 
	STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=8%};

    DEFINE COL5/ "Ongoing?"
	STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=8%};

    DEFINE COL6/ "Clinically Significant?"
	STYLE(HEADER)={JUST=L ASIS=ON} STYLE(COLUMN)={JUST=L ASIS=ON CELLWIDTH=12%};

    BREAK AFTER C/PAGE;

	FOOTNOTE J=L FONT='COURIER NEW' HEIGHT=9PT 
	"Note: M = Male; F = Female. Cohort 1: Caucasian; Cohort 2: Japanese.";
	FOOTNOTE J=L FONT='COURIER NEW' HEIGHT=9PT 
	"PATH.sas ran on &sysdate at &systime Programmer: &sysuserid ";
RUN; 
ODS RTF CLOSE;

/********************************************************************************************/
